#include "VertexOutputs.hlsl"
#include "UniformBuffers.hlsl"
#include "NormalMapping.hlsl"
#include "PBR.hlsl"
#include "Utilities.hlsl"


[[vk::binding(0)]]
Texture2D<float2> LinearDepth;

[[vk::binding(1)]]
Texture2D<float> Depth;

[[vk::binding(2)]]
Texture2D<float2> Normals;

[[vk::binding(3)]]
Texture2D<float4> specularRoughness;

[[vk::binding(4)]]
Texture2D<float4> previousColour;

[[vk::binding(5)]]
Texture2D<float2> velocity;

[[vk::binding(6)]]
TextureCube<float4> skybox;

[[vk::binding(7)]]
SamplerState linearSampler;

[[vk::binding(8)]]
ConstantBuffer<CameraBuffer> camera;

[[vk::binding(9)]]
RWTexture2D<float4> reflections;

#define START_MIP 7

#include "RayMarching.hlsl"

[numthreads(8, 8, 1)]
void main(uint3 globalIndex : SV_DispatchThreadID)
{
	const float2 uv = (float2(globalIndex.xy) / (camera.frameBufferSize / 2.0f)) + camera.jitter;

	const float fragmentDepth = Depth.Sample(linearSampler, uv);

	if(fragmentDepth == 0.0f) // skybox.
	{
        reflections[globalIndex.xy] = float4(0.0f, 0.0f, 0.0f, 1.0f);
		return;
	}

	float4 positionWS =  mul(camera.invertedViewProj, float4((uv - 0.5f) * 2.0f, fragmentDepth, 1.0f));
	positionWS /= positionWS.w;

	float3 position = float3((uv - 0.5f) * 2.0f, fragmentDepth);

	float3 view = normalize(camera.position - positionWS.xyz);

	float3 normal = decodeOct(Normals.Load(int3(globalIndex.xy * 2, 0)));

	uint width, height;
	LinearDepth.GetDimensions(width, height);
	width >>= START_MIP;
	height >>= START_MIP;

	const float3 L = reflect(-view, normal);

	const float max_distance = max(max(camera.sceneSize.x, camera.sceneSize.y), camera.sceneSize.z);
	float4 rayEnd = float4(positionWS + (L * max_distance), 1.0f);
	rayEnd = mul(camera.viewProj, rayEnd);
	rayEnd /= rayEnd.w;

	const float3 rayDirection = normalize(rayEnd.xyz - position.xyz);

	// March the ray.
    float2 colourUV = marchRay(position, rayDirection, 30, float2(1.0f, 1.0f) / float2(width, height), START_MIP, camera.nearPlane, camera.farPlane, camera.invertedPerspective);
    float2 currentVelocity = velocity.Sample(linearSampler, uv);
    currentVelocity = currentVelocity - 0.5f;
    colourUV = colourUV - currentVelocity;

    const float roughness = specularRoughness.Sample(linearSampler, uv).w;
    const float roughnessMip = roughness * 5.0f;

    float4 colour;
    if(all(colourUV >= float2(0.0f, 0.0f)))
    {
        colour = previousColour.SampleLevel(linearSampler, colourUV, roughnessMip);
    }
    else
    {
        colour = skybox.SampleLevel(linearSampler, rayDirection, roughnessMip * 2.0f);
    }

    reflections[globalIndex.xy] = colour;
}

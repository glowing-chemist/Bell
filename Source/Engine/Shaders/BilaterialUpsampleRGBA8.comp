#include "NormalMapping.hlsl"

[[vk::binding(0)]]
Texture2D<float4> Input;

[[vk::binding(1)]]
RWTexture2D<float4> Output;

[[vk::binding(2)]]
SamplerState linearSampler;

[[vk::binding(3)]]
SamplerState pointSampler;

[[vk::binding(4)]]
Texture2D<float2> linearDepth;

[[vk::binding(5)]]
Texture2D<float2> Normals;


float4 nearestDepthSample(const uint2 pixel, uint2 outputSize, Texture2D<float4> input, Texture2D<float2> depth)
{
	uint inputWidth, inputHeight;
	input.GetDimensions(inputWidth, inputWidth);

	const float minInputSize = min(inputWidth, inputHeight);
	const float minOutputSize = min(outputSize.x, outputSize.y);

	const float depthMip = log2(minOutputSize) - log2(minInputSize);

	float depthSamples[4] = 
	{
		depth.Load(uint3(pixel + int2(0, 0), depthMip)).x,
		depth.Load(uint3(pixel + int2(0, 1), depthMip)).x,
		depth.Load(uint3(pixel + int2(1, 0), depthMip)).x,
		depth.Load(uint3(pixel + int2(1, 1), depthMip)).x
	};

	int2 inputOffsets[4] = 
	{
		int2(0, 0),
		int2(0, 1),
		int2(1, 0),
		int2(1, 1)
	};

	float d0 = depth.Load(uint3(pixel, 0)).x;
	float minDist = 1.0f;

	int bestDepthIndex = 0;
	for (int i = 0; i < 4; i++) {
		const float dist = abs(d0 - depthSamples[i]);

		if (minDist > dist) {
			minDist = dist;
			bestDepthIndex = i;
		}
	}

	return input.Load(uint3(pixel + inputOffsets[bestDepthIndex], 0));
}


[numthreads(16, 16, 1)]
void main(uint3 globalIndex : SV_DispatchThreadID)
{
	uint width, height;
	Output.GetDimensions(width, height);

	if(globalIndex.x >= width || globalIndex.y >= height)
		return;

	// Sample the normal map to detect face discontinuties.
	float3 n[4] = 
	{
		decodeOct(Normals.Load(int3(globalIndex.xy + int2(0, 0), 0))),
		decodeOct(Normals.Load(int3(globalIndex.xy + int2(0, 1), 0))),
		decodeOct(Normals.Load(int3(globalIndex.xy + int2(1, 0), 0))),
		decodeOct(Normals.Load(int3(globalIndex.xy + int2(1, 1), 0)))
	};

	float dot01 = dot(n[0], n[1]);
	float dot02 = dot(n[0], n[2]);
	float dot03 = dot(n[0], n[3]);

	float minDot = min(dot01, min(dot02, dot03));
	float s = step(0.997, minDot);

	Output[globalIndex.xy] = lerp(nearestDepthSample(globalIndex.xy / uint2(2, 2), uint2(width, height), Input, linearDepth), Input.Load(uint3(globalIndex.xy / uint2(2, 2), 0)), s);
} 
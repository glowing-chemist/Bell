#version 450
#extension GL_ARB_separate_shader_objects : enable
#extension GL_GOOGLE_include_directive : enable

#include "ClusteredLighting.glsl"
#include "UniformBuffers.glsl"


layout(std430, set = 0,  binding = 0) buffer visibleFroxlesCounter
{
	int visibleCounter;
};

layout(std430, set = 0,  binding = 1) buffer visibleFroxles
{
	uvec4 froxel[];
};

layout(std430, set = 0,  binding = 2) buffer froxelIndixCounter
{
	uint indexCounter;
};

layout(std430, set = 0,  binding = 3) buffer froxelIndicies
{
	uint indicies[];
};

layout(std430, set = 0, binding = 4) buffer sparseFroxels
{
	uvec2 sparseFroxelList[];
};

layout(set =0, binding = 5) uniform cameraBuffer
{
	CameraBuffer camera;
};

layout(std430, set = 1, binding = 0) buffer sceneLights
{
	uvec4  lightCount;
	Light lights[];
};

layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;


void main()
{
	const int froxelIndex = atomicAdd(visibleCounter, -1) - 1; // need the value after the opertation.

	if(froxelIndex < 0)
		return;

	const uvec3 froxelToProcess = froxel[froxelIndex].xyz;

	const AABB aabb = getFroxelAABB(froxelToProcess, camera.fov, camera.frameBufferSize, camera.nearPlane, camera.farPlane);

	uint nextFreeSlot = 0;
	uint lightIndicies[LIGHTS_PER_FROXEL];

	for(uint i = 0; i < lightCount.x; ++i)
	{
		const Light light = lights[i];
		switch(uint(light.type))
		{
			case 0:
			{
				const bool intersects = sphereAABBIntersection(light.position.xyz - camera.position, light.influence, aabb);
				if(intersects)
				{
					lightIndicies[nextFreeSlot++] = i;
				}
				break;
			}

			// TODO add more light types as and when supported.

			// Should never hit here.
			default:
				continue;
		}

		// Don't evaluate any more light if we have asigned all.
		if(nextFreeSlot == LIGHTS_PER_FROXEL)
			break;
	}

	// Update the global data structures.
	uint startLightIndex = atomicAdd(indexCounter, nextFreeSlot);

	for(uint i = 0; i < nextFreeSlot; ++i)
	{
		indicies[startLightIndex + i] = lightIndicies[i];
	}

	sparseFroxelList[getFroxelIndex(froxelToProcess, uvec2(camera.frameBufferSize))] = uvec2(startLightIndex, nextFreeSlot);
}
